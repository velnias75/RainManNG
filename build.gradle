plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'java'

archivesBaseName = 'RainManNG'
version = '1.3-SNAPSHOT'

repositories {
	mavenLocal()

	maven { url = uri('https://papermc.io/repo/repository/maven-public/') }
	maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
	maven { url = 'https://oss.sonatype.org/content/repositories/central' }
	
	mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

dependencies {

	compileOnly "org.bukkit:bukkit:${project.bukkit_version}"

	compileOnly("org.spigotmc:plugin-annotations:${project.plugins_annotation_version}") {
		exclude module: "bukkit"
	}
	annotationProcessor "org.spigotmc:plugin-annotations:${project.plugins_annotation_version}"
	
	implementation "org.bstats:bstats-bukkit:${project.bstats_version}"
	shadow "org.bstats:bstats-bukkit:${project.bstats_version}"
	
	compileOnly "com.google.code.findbugs:annotations:${project.findbugs_version}"
	annotationProcessor "com.google.code.findbugs:annotations:${project.findbugs_version}"
	
	testImplementation "org.bukkit:bukkit:${project.bukkit_version}"
	testImplementation "junit:junit:${project.junit_version}"
}

shadowJar {

	archiveClassifier.set('')    
    relocate 'org.bstats', 'de.rangun.RainManNG.shadowed.org.bstats'

    //Exclude META-INF entries of shaded deps
    exclude 'META-INF/**'
    exclude '**/*.pdn'
    exclude '**/*.bbmodel'
    from('.') {
        include 'LICENSE.md'
		include 'NOTICE.txt'
		include 'licenses/**'
    }
    minimize()
}

assemble {
	dependsOn shadowJar
}

test {
    testLogging {
      events "failed"

      showExceptions true
      exceptionFormat "full"
      showCauses true
      showStackTraces true

      showStandardStreams = false
    }
  }
